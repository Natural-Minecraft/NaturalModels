import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar
import com.vanniktech.maven.publish.SourcesJar
import kotlin.io.encoding.Base64

plugins {
    id("standard-conventions")
    id("com.vanniktech.maven.publish")
    signing
}

val gitHubPackagesId = rootProject.name.lowercase()
val artifactBaseId = "$gitHubPackagesId-$name"
val artifactVersion = project.version.toString().run {
    BUILD_NUMBER?.let { substringBeforeLast("-$it") } ?: this
}

java {
    withSourcesJar()
    withJavadocJar()
}

signing {
    val key = System.getenv("SIGNING_KEY")?.let {
        Base64.decode(it.toByteArray()).toString(Charsets.UTF_8)
    }
    val password = System.getenv("SIGNING_PASSWORD")
    if (!key.isNullOrEmpty() && !password.isNullOrEmpty()) {
        useInMemoryPgpKeys(
            key,
            password
        )
    } else useGpgCmd()
}

dependencies {
    api(libs.bundles.library)

    compileOnly(libs.lombok)
    annotationProcessor(libs.lombok)

    testCompileOnly(libs.lombok)
    testAnnotationProcessor(libs.lombok)
}

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()
    coordinates("io.github.toxicity188", artifactBaseId, artifactVersion)
    configure(JavaLibrary(
        javadocJar = JavadocJar.None(),
        sourcesJar = SourcesJar.Sources(),
    ))
    pom {
        name = artifactBaseId
        description = "Modern Bedrock model engine for Minecraft Java Edition"
        inceptionYear = "2024"
        url = "https://github.com/toxicity188/NaturalModels/"
        licenses {
            license {
                name = "MIT License"
                url = "https://mit-license.org/"
            }
        }
        developers {
            developer {
                id = "toxicity188"
                name = "toxicity188"
                url = "https://github.com/toxicity188/"
            }
        }
        scm {
            url = "https://github.com/toxicity188/NaturalModels/"
            connection = "scm:git:git://github.com/toxicity188/NaturalModels.git"
            developerConnection = "scm:git:ssh://git@github.com/toxicity188/NaturalModels.git"
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/toxicity188/$gitHubPackagesId")
            credentials {
                username = "toxicity188"
                password = System.getenv("PACKAGES_API_TOKEN")
            }
        }
    }
}

