import xyz.jpenilla.resourcefactory.bukkit.Permission
import xyz.jpenilla.resourcefactory.paper.PaperPluginYaml

plugins {
    alias(libs.plugins.convention.plugin)
    alias(libs.plugins.resourcefactory.paper)
}

val libraryDir: Provider<RegularFile> = layout.buildDirectory.file("generated/paper-library")

dependencies {
    shade(project(":nms:v1_21_R1")) { isTransitive = false }
    shade(project(":nms:v1_21_R3")) { isTransitive = false }
    shade(project(":nms:v1_21_R4")) { isTransitive = false }
    shade(project(":nms:v1_21_R5")) { isTransitive = false }
    shade(project(":nms:v1_21_R6")) { isTransitive = false }
    shade(project(":nms:v1_21_R7")) { isTransitive = false }
}

modrinth {
    loaders = PAPER_LOADERS
}

tasks.modrinth {
    dependsOn(tasks.modrinthSyncBody)
}

val generatePaperLibrary by tasks.registering {
    outputs.file(libraryDir)
    doLast {
        val file = libraryDir.get().asFile
        file.parentFile.mkdirs()
        file.writeText(libs.bundles.library.get().joinToString("\n") { dep -> dep.toString() })
    }
}

tasks.shadowJar {
    archiveFileName.set("NaturalModels.jar")
    archiveBaseName.set("NaturalModels")
    archiveClassifier.set("")
    archiveVersion.set("")
    destinationDirectory.set(layout.buildDirectory.dir("libs"))
    dependsOn(generatePaperLibrary)
    from(libraryDir)
    manifest {
        attributes["paperweight-mappings-namespace"] = "mojang"
    }
}

paperPluginYaml {
    main = "$group.paper.NaturalModelsPaper"
    loader = "$group.paper.NaturalModelsLoader"
    version = project.version.toString()
    name = rootProject.name
    foliaSupported = true
    apiVersion = "1.20"
    author = "NaturalModels"
    contributors = listOf("https://github.com/toxicity188/NaturalModels/graphs/contributors")
    description = "Modern Bedrock model engine for Minecraft Java Edition"
    website = "https://modrinth.com/plugin/NaturalModels"
    dependencies {
        server(
            name = "MythicMobs",
            required = false,
            load = PaperPluginYaml.Load.BEFORE
        )
        server(
            name = "Citizens",
            required = false,
            load = PaperPluginYaml.Load.BEFORE
        )
        server(
            name = "SkinsRestorer",
            required = false,
            load = PaperPluginYaml.Load.BEFORE
        )
        server(
            name = "Nexo",
            required = false,
            load = PaperPluginYaml.Load.OMIT
        )
    }
    permissions.create("NaturalModels") {
        default = Permission.Default.OP
        description = "Accesses to command."
        children = mapOf(
            "reload" to true,
            "spawn" to true,
            "disguise" to true,
            "undisguise" to true,
            "test" to true,
            "play" to true,
            "version" to true,
            "hide" to true,
            "show" to true
        )
    }
}

